class MutcIO;

classtype int;

use ArrayArith: { +, sel, <};
use ArrayBasics: { dim, shape};
use String: { string};

export { MutcIO, FibrePrint, print, mutcWorld, 
           FibreScanIntArray, FibreScanDoubleArray};

objdef MutcIO mutcWorld = to_MutcIO( 0);

external void svp_io_puts( string a);
#pragma effect MutcIO::mutcWorld
#pragma mutcthreadfun

external void svp_io_putf( double d, int places, int base);
#pragma effect MutcIO::mutcWorld
#pragma mutcthreadfun

external void sac_svp_io_putn( int num, int base);
#pragma effect MutcIO::mutcWorld
#pragma mutcthreadfun

external int[*] read_int( );
#pragma effect MutcIO::mutcWorld         
#pragma linkobj "src/MutcIO/read.o"             
#pragma linksign[1] 
#pragma refcounting[0] 

external double[*] read_double( );
#pragma effect MutcIO::mutcWorld         
#pragma linkobj "src/MutcIO/read.o"             
#pragma linksign[1]                    
#pragma refcounting[0] 

#define UNTYPED( type, name)                                         \
inline                                                               \
void print( type[*] array){                                          \
  print(array, 0);                                                   \
}                                                                    \
                                                                     \
inline                                                               \
void FibrePrint( type[+] array){                                     \
  FibrePrint( array, 0);                                             \
}                                                                    \
                                                                     \
inline                                                               \
void FibrePrint( type scaler){                                       \
  FibrePrint( scaler, 0);                                            \
}                                                                    \
                                                                     \
inline                                                               \
void FibrePrint( type scaler, int indent){                           \
  print( scaler, indent);                                            \
}                                                                    \
                                                                     \
inline                                                               \
void print( type[+] array, int indent){                              \
  indent(indent);                                                    \
  svp_io_puts("[\n");                                                \
                                                                     \
  for ( i=0; i < shape(array)[0]; i++){                              \
    print( array[i], indent + 1);                                    \
  }                                                                  \
                                                                     \
  indent(indent);                                                    \
  svp_io_puts("]\n");                                                \
}                                                                    \
                                                                     \
inline                                                               \
void FibrePrint( type[+] array, int indent){                         \
  indent( indent);                                                   \
  svp_io_puts( "[1,");                                               \
  sac_svp_io_putn( shape(array)[0], 10);                             \
  svp_io_puts( ":\n");                                               \
                                                                     \
  for (i=0; i< shape(array)[0]; i++){                                \
    FibrePrint( array[i], indent+1);                                 \
  }                                                                  \
                                                                     \
  indent( indent);                                                   \
  svp_io_puts( "]\n");                                               \
}                                                                    \
                                                                     \
type[*] FibreScan##name##Array( ){                                   \
  return( read_##type( ));                                           \
}

UNTYPED( int, Int)
UNTYPED( double, Double)

inline
void print( int scaler, int indent){
  indent( indent);
  sac_svp_io_putn( scaler, 10);
  svp_io_puts( "\n");
}

inline
void print( double scaler, int indent){
  indent( indent);
  svp_io_putf( scaler, 16, 10);
  svp_io_puts( "\n");
}

inline
void print( string scaler, int indent){
  indent( indent);
  svp_io_puts( scaler);
}

/*
 * Special cases where arrays of type are not supported
 */
inline 
void print( string scaler){
  print( scaler, 0);
}

/* 
 * Indent [indent] amount
 */
inline
void indent( int indent){
  for ( i=0; i<indent; i++){
    svp_io_puts("  ");
  }
}
